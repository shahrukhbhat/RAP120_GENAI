 managed implementation in class ZBP_R_TRAVEL_SB1 unique;
 strict ( 2 );
 with draft;
 define behavior for ZR_TRAVEL_SB1 alias Travel
 persistent table ztravel_sb1
 draft table ztravel_sb1_d
 etag master LocalLastChangedAt
 lock master total etag LastChangedAt
 authorization master ( global )

 {
   field ( mandatory : create )
   TravelId,
   Destination;

   field ( mandatory )
   CustomerID;

   field ( readonly )
   CreatedBy,
   CreatedAt,
   LocalLastChangedBy,
   LocalLastChangedAt,
   LastChangedAt;

   field ( readonly : update )
   TravelId;

   create;
   update;
   delete;

   validation validateCustomer on save { create; field CustomerID; }
   determination setInitialTravelStatus on save { create; }
   determination setDescription on modify { create; }
   determination setBookingFee on modify { create; update; }

   determine action checkCustomer
   {
     validation validateCustomer;
   }

   draft action Activate optimized;
   draft action Discard;
   draft action Edit;
   draft action Resume;
   draft determine action Prepare
   {
     validation validateCustomer;
   }

   mapping for ztravel_sb1
     {
       TravelId           = travel_id;
       AgencyId           = agency_id;
       CustomerId         = customer_id;
       BeginDate          = begin_date;
       EndDate            = end_date;
       Destination        = destination;
       BookingFee         = booking_fee;
       TotalPrice         = total_price;
       CurrencyCode       = currency_code;
       Description        = description;
       Status             = status;
       CreatedBy          = created_by;
       CreatedAt          = created_at;
       LocalLastChangedBy = local_last_changed_by;
       LocalLastChangedAt = local_last_changed_at;
       LastChangedAt      = last_changed_at;
     }
 }